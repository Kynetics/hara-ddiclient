/*
 * Copyright Â© 2017-2019 Kynetics LLC
 *
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 *
 */

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
}

repositories {
    mavenCentral()
}

final def deps = gradle.settings.app_dependencies
final def tdeps = gradle.settings.test_dependencies

group 'com.kynetics.updatefactory'
version '0.4.3'

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    repositories {
        mavenCentral()
    }
    dependencies {
        implementation deps.kotlin_stdlib
        implementation deps.kotlin_x
        //runtime deps.kotlin_compiler
    }
}

project(':ddiapi-client') {
    final def subprj = ':ddiapi-client:ddiapi-client-api'
    dependencies {
        implementation project(subprj)
        implementation deps.retrofit_converter
    }
    allprojects {
        dependencies {
            implementation deps.okhttp
            implementation deps.retrofit
            implementation deps.retrofit_coroutines
        }
    }
    project(subprj) {
        dependencies {
            implementation deps.gson
            testCompile tdeps.testng
        }
        test {
            useTestNG()
        }

    }
}

project(':core') {
    dependencies {
        implementation project(':ddiapi-client')
        implementation project(':ddiapi-client:ddiapi-client-api')
        implementation deps.retrofit
    }
}

task listConfigurations <<{
    allprojects.each { p ->
        println(p.name)
        p.configurations.each { c ->
            println("\t${c.name}: ${c.description}")
        }
    }
}